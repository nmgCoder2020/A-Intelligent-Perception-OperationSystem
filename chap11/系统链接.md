##11.2 系统链接

之所以把系统的链接脚本单独放成一小节，就是因为链接脚本这里实在确实有点抽象，对于初学者确实不友好，所以我想链接脚本这里单独的写成一个小节。


###首先我们来看看什么是链接脚本？

>以下英文材料均来自GNU官网。https://sourceware.org/binutils/docs/ld/      我只做简单翻译，谬误之处请指正。

>Linker Scripts

>Every link is controlled by a linker script. This script is written in the linker command language.
>The main purpose of the linker script is to describe how the sections in the input files should be mapped into the output file, and to control the memory layout of the output file. Most linker scripts do nothing more than this. However, when necessary, the linker script can also direct the linker to perform many other operations, using the commands described below.
>The linker always uses a linker script. If you do not supply one yourself, the linker will use a default script that is compiled into the linker executable. You can use the `--verbose' command line option to display the default linker script. Certain command line options, such as `-r' or `-N', will affect the default linker script.

>You may supply your own linker script by using the `-T' command line option. When you do this, your linker script will replace the default linker script.

>You may also use linker scripts implicitly by naming them as input files to the linker, as though they were files to be linked.

链接器脚本：

每次链接操作都是通过链接脚本来控制的，这是一种用来写链接器命令的语言。链接脚本的主要目的是用来描述每个输入文件的节应该如何映射到输出文件，并且控制输出文件在内存中的布局，几乎所有的链接教本只做这些事。然而，当需要时链接脚本也可以引导链接器做一些其它的操作，使用下面的命令。链接器总是要使用链接脚本的，如果你不提供自己制作的脚本，链接器将会使用一个默认的链接脚本，确定的命令行选项例如“-r” "-N" ,不然将会使用默认的链接脚本。


###基础的链接脚本概念：

>We need to define some basic concepts and vocabulary in order to describe the linker script language.

我们需要定义基本的概念和术语，用来描述链接脚本语言。

>The linker combines input files into a single output file. The output file and each input file are in a special data format known as an object file format. Each file is called an object file. The output file is often called an executable, but for our purposes we will also call it an object file. Each object file has, among other things, a list of sections. We sometimes refer to a section in an input file as an input section; similarly, a section in the output file is an output section.

链接器将多个输入文件结合起来组整成一个输出文件。输出文件和每一个输入文件由一种特殊的数据格式就是目标文件。每一个文件都是目标文件，这个书出文件经常被叫做可执行文件，但从我们的角度来说我们更愿意叫它目标文件，每个目标文件都包含很多其它的东西，一个节的列表。我们有时将输入文件的一个节作为一个输入节。类似的，一个在书出文件中的节就是一个输出节。

>Each section in an object file has a name and a size. Most sections also have an associated block of data, known as the section contents. A section may be marked as loadable, which means that the contents should be loaded into memory when the output file is run. A section with no contents may be allocatable, which means that an area in memory should be set aside, but nothing in particular should be loaded there (in some cases this memory must be zeroed out). A section which is neither loadable nor allocatable typically contains some sort of debugging information.

每一个目标文件中的节有一个名字和长度，几乎所有的节都关联一个数据块，就是节的内容，一个节可能被描述成可加载的，这意味着它的上下文将会被加载到内存中，输出运行的地方。一个节没有内容
可能是动态分配的，这意味着它的空间需要被在内存中预留，但是没有其它的部分被加载到哪里（在一些情况下，这些内容必须被清0），一个节不具备加载属性也不具备分配属性一般和调试信息有关。

>Every loadable or allocatable output section has two addresses. The first is the VMA, or virtual memory address. This is the address the section will have when the output file is run. The second is the LMA, or load memory address. This is the address at which the section will be loaded. In most cases the two addresses will be the same. An example of when they might be different is when a data section is loaded into ROM, and then copied into RAM when the program starts up (this technique is often used to initialize global variables in a ROM based system). In this case the ROM address would be the LMA, and the RAM address would be the VMA.

每一段的加载或者分配的输出节需要有两个地址，第一个地址是VMA(虚拟地址)，或者说是虚拟地址。这个地址是节未来在输出文件加载时运行的地址。第二个地址是LMA，或者书是加载地址，这是节为来要被加载的地址。在绝大多数情况下两个地址是相同的，一个他们可能不同的例子是当一个数据节被加载到ROM(只读存储器），并且将拷贝到RAM，当程序启动的时候，（这种技术经常被是用来初始化ROM中的全局变量在系统中），在这种情况下，ROM的地址就是加载地址，并且内存的地址是虚拟地址。
注释：LMA和VMA不同的情况多发生在代码烧在板子上的情况，当系统或者代码运行时如发现他们不同，就会把LMA加载到VMA处。

一个博客的通俗解释：http://longer.spaces.eepw.com.cn/articles/trackback/item/71565

>You can see the sections in an object file by using the objdump program with the `-h' option.
你可以使用命令objdump -h 来察看程序中各个节的位置。

>Every object file also has a list of symbols, known as the symbol table. A symbol may be defined or undefined. Each symbol has a name, and each defined symbol has an address, among other information. If you compile a C or C++ program into an object file, you will get a defined symbol for every defined function and global or static variable. Every undefined function or global variable which is referenced in the input file will become an undefined symbol.
每一个目标文件都还有一个符号表，一个符号可能被定义或者没有定义。每一个符号都有名字，和定义的地址，包括的内容信息。如果是编译一个C 或者C++程序在目标文件中，你将会得到一个被定义的符号表其中包含了每一个函数和全局的或者静态变量。每一个未定义的全局符号在输出文件中是未定义符号。

>You can see the symbols in an object file by using the nm program, or by using the objdump program with the `-t' option.
你可以使用nm 或者 objdump -t 来查看您的符号表。

###链接脚本格式
>Linker scripts are text files.
链接脚本就是一个文本文件。

>You write a linker script as a series of commands. Each command is either a keyword, possibly followed by arguments, or an assignment to a symbol. You may separate commands using semicolons. Whitespace is generally ignored.
你可以写一个链接脚本来指定一系列命令。每一个命令都是关键字，后边可能是参数，或者是参数的符号，你可以使用分好分隔命令，空格一般被忽略。

###链接脚本的例子

链接脚本几乎都是一样的。

最简单的链接脚本只有一个关键字“SECTION”，你可以使用它来定义输出文件的内存布局。这条指令十一条很有用的指令，这里用来描述一个简单的使用，我们的程序仅仅只包含代码，初始化数据
，为初始化数据段，标识为".text"  ".data" ".bss",我们假设未来只有这一个节在输出文件中。

看下面的例子，我们假设代码段被加载到0x10000 ,并且数据段被加载到0x8000000的位置。
```
     SECTIONS
     {
       . = 0x10000;
       .text : { *(.text) }
       . = 0x8000000;
       .data : { *(.data) }
       .bss : { *(.bss) }
     }

```
你可以写一个对关键字，然后用大括号括起后边的内容与符号。每一行开始的"."是一个计数器，如果你没有特殊指定位置（其它的方法后边说），这个地址就会被设置为一个偏移量。然后偏移量增大，开始的位置偏移量为0.第二行的输出节".text" 冒号是必须的语法，在后边的大括号里填入需要放入到此节的名称。* 是一个统配符，表达式 *（.text)是表示所有的输入节。链接器将会把文本段定地址写为0x10000. 同样的数据段就会被定义到0x80000000的位置在输出文件中。链接器将通过增加位置计数器（如果需要）确保每个输出段具有所需的对齐。在本例中，`.text'和`.data'节的指定地址可能满足任何对齐约束，但链接器可能必须在`.data'和`.bss'节之间创建一个小间隙。

###简单的命令

####入口地址
设置入口地址：

    定义程序开始的地方使用ENTRY(symbol)
    
以下是几个注意的点：
>the `-e' entry command-line option;
>the ENTRY(symbol) command in a linker script;
>the value of a target specific symbol, if it is defined; For many targets this is start, but PE and BeOS based systems for example check a list of possible entry symbols, matching the first one found.
>the address of the first byte of the `.text' section, if present;
>The address 0.

####处理文件的命令

INCLUDE filename  包含文件的命令

INPUT（file,file .....) 输入文件的命令

OUTPUT(filename)   输出文件的命令

####处理目标文件的命令

OUTPUT_FORMAT(bfdname)

OUTPUT_FORMAT(default, big, little)

一个小例子链接MIPS ELF文件

OUTPUT_FORMAT(elf32-bigmips, elf32-bigmips, elf32-littlemips)
 
####内存对齐的命令

别名映射：
     REGION_ALIAS(alias, region)
     这种用法主要是因为有的机器有很多的存储设备，并且程序的不同内容需要使用不同的存储设备。
     
例如我们有这种需求：
.text program code;
.rodata read-only data;
.data read-write initialized data;
.bss read-write zero initialized data.

不同的段在不同的位置，简而言之，是这样：

|Section	|Variant A	|Variant B	|Variant C|
|----------:|:----------|:----------|:--------| 
|.text	|RAM	|ROM	|ROM 
|.rodata|RAM	|ROM	|ROM2 
|.data	|RAM	|RAM/ROM|RAM/ROM2 
|.bss	|RAM	|RAM	|RAM 

那么，我们的链接脚本需要包含一个说明文档

```
  INCLUDE linkcmds.memory
     
     SECTIONS
       {
         .text :
           {
             *(.text)
           } > REGION_TEXT
         .rodata :
           {
             *(.rodata)
             rodata_end = .;
           } > REGION_RODATA
         .data : AT (rodata_end)
           {
             data_start = .;
             *(.data)
           } > REGION_DATA
         data_size = SIZEOF(.data);
         data_load_start = LOADADDR(.data);
         .bss :
           {
             *(.bss)
           } > REGION_BSS
       }
```

Now we need three different linkcmds.memory files to define memory regions and alias names. The content of linkcmds.memory for the three variants A, B and C:


A

Here everything goes into the RAM.

          MEMORY
            {
              RAM : ORIGIN = 0, LENGTH = 4M
            }
          
          REGION_ALIAS("REGION_TEXT", RAM);
          REGION_ALIAS("REGION_RODATA", RAM);
          REGION_ALIAS("REGION_DATA", RAM);
          REGION_ALIAS("REGION_BSS", RAM);
     

B

Program code and read-only data go into the ROM. Read-write data goes into the RAM. An image of the initialized data is loaded into the ROM and will be copied during system start into the RAM.

          MEMORY
            {
              ROM : ORIGIN = 0, LENGTH = 3M
              RAM : ORIGIN = 0x10000000, LENGTH = 1M
            }
          
          REGION_ALIAS("REGION_TEXT", ROM);
          REGION_ALIAS("REGION_RODATA", ROM);
          REGION_ALIAS("REGION_DATA", RAM);
          REGION_ALIAS("REGION_BSS", RAM);
     


C
Program code goes into the ROM. Read-only data goes into the ROM2. Read-write data goes into the RAM. An image of the initialized data is loaded into the ROM2 and will be copied during system start into the RAM.

          MEMORY
            {
              ROM : ORIGIN = 0, LENGTH = 2M
              ROM2 : ORIGIN = 0x10000000, LENGTH = 1M
              RAM : ORIGIN = 0x20000000, LENGTH = 1M
            }
          
          REGION_ALIAS("REGION_TEXT", ROM);
          REGION_ALIAS("REGION_RODATA", ROM2);
          REGION_ALIAS("REGION_DATA", RAM);
          REGION_ALIAS("REGION_BSS", RAM);
          
     
It is possible to write a common system initialization routine to copy the .data section from ROM or ROM2 into the RAM if necessary:


     #include <string.h>
     
     extern char data_start [];
     extern char data_size [];
     extern char data_load_start [];
     
     void copy_data(void)
     {
       if (data_start != data_load_start)
         {
           memcpy(data_start, data_load_start, (size_t) data_size);
         }
     }

就是这样，我发现LEX YACC LD 都是一个画风的软件啊。

####其它命令
写两个我们用的上的。

断言：判断映射是否正确，很简单
ASSERT(exp,message)
```
  .stack :
            {
              PROVIDE (__stack = .);
              PROVIDE (__stack_size = 0x100);
              ASSERT ((__stack > (_end + __stack_size)), "Error: No room left for the stack");
            }
     
```
EXTERN(symbol symbol ...)
    引用符号。
 
####为符号分配值

链接脚本也是可计算的：
You may assign to a symbol using any of the C assignment operators:

symbol = expression ;

symbol += expression ;

symbol -= expression ;

symbol *= expression ;

symbol /= expression ;

symbol <<= expression ;

symbol >>= expression ;

symbol &= expression ;

symbol |= expression ;


例子：
```
  floating_point = 0;
     SECTIONS
     {
       .text :
         {
           *(.text)
           _etext = .;
         }
       _bdata = (. + 3) & ~ 3;
       .data : { *(.data) }
     }
```
链接脚本支持基本的C操作。

####链接脚本也可以覆盖内存

链接描述文件可能包含对MEMORY命令的许多使用，但是，定义的所有内存块都被视为在单个MEMORY命令中指定。
MEMORY的语法是：

```
MEMORY
       {
         name [(attr)] : ORIGIN = origin, LENGTH = len
         ...
       }
```
The attr string must consist only of the following characters:

`R'
Read-only section 
`W'
Read/write section 
`X'
Executable section 
`A'
Allocatable section 
`I'
Initialized section 
`L'
Same as `I' 
`!'
Invert the sense of any of the attributes that follow   

例如：

```
     MEMORY
       {
         rom (rx)  : ORIGIN = 0, LENGTH = 256K
         ram (!rx) : org = 0x40000000, l = 4M
       }
```

####PHDRS 命令
ELF对象文件格式使用程序头，也称为段。程序头描述了如何将程序加载到内存中。您可以使用带-p选项的objdump程序将其打印出来。
```
   PHDRS
     {
       name type [ FILEHDR ] [ PHDRS ] [ AT ( address ) ]
             [ FLAGS ( flags ) ] ;
     }
```
这是一个简单的程序头：

‵‵‵
Here is an example of PHDRS. This shows a typical set of program headers used on a native ELF system.

     PHDRS
     {
       headers PT_PHDR PHDRS ;
       interp PT_INTERP ;
       text PT_LOAD FILEHDR PHDRS ;
       data PT_LOAD ;
       dynamic PT_DYNAMIC ;
     }
     
     SECTIONS
     {
       . = SIZEOF_HEADERS;
       .interp : { *(.interp) } :text :interp
       .text : { *(.text) } :text
       .rodata : { *(.rodata) } /* defaults to :text */
       ...
       . = . + 0x1000; /* move to a new page in memory */
       .data : { *(.data) } :data
       .dynamic : { *(.dynamic) } :data :dynamic
       ...
     }
‵‵‵
####版本描述

     VERSION { version-script-commands }
     
```
 VERS_1.1 {
     	 global:
     		 foo1;
     	 local:
     		 old*;
     		 original*;
     		 new*;
     };
     
     VERS_1.2 {
     		 foo2;
     } VERS_1.1;
     
     VERS_2.0 {
     		 bar1; bar2;
     	 extern "C++" {
     		 ns::*;
     		 "f(int, double)";
     	 };
     } VERS_1.2;
```
####表达式

链接描述语言中的表达式的语法与C表达式的语法相同。所有表达式都将作为整数进行求值。所有表达式以相同的大小进行评估，如果主机和目标都是32位，则为32位，否则为64位。


